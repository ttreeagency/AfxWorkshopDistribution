prototype(Ttree.AfxWorkshop:Component.Atom.Circle) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Circle'

        props {
            backgroundColor = '#eeeeee'
            circleColor = '#FDA04F'
            strokeWidth = '4'
        }

        propSets {
            'Light Blue' {
                backgroundColor = '#eeeeee'
                circleColor = '#90caf9'
            }
            'Deep Purple' {
                backgroundColor = '#eeeeee'
                circleColor = '#b39ddb'
            }
        }
    }

    backgroundColor = '#eeeeee'
    circleColor = '#FDA04F'
    strokeWidth = '4'

    _dimension = ${(40 + 2 * String.toInteger(this.strokeWidth)) + 'px'}
    _innerPosition = ${(20 + String.toInteger(this.strokeWidth)) + 'px'}
    _radius = ${(20 + (String.toInteger(this.strokeWidth) / 2)) + 'px'}

    renderer = afx`
        <svg width={props._dimension} height={props._dimension}>
            <circle class="path-bg" cx={props._innerPosition} cy={props._innerPosition} r={props._radius} fill="none" stroke-width={props.strokeWidth} stroke={props.backgroundColor} @key="background"/>
            <circle class="path" cx={props._innerPosition} cy={props._innerPosition} r={props._radius} fill="none" stroke-width={props.strokeWidth} stroke-miterlimit="10" stroke={props.circleColor} @key="circle"/>
        </svg>
    `
}
