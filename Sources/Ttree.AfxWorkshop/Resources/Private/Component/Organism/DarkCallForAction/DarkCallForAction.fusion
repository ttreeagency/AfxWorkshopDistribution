prototype(Ttree.AfxWorkshop:Component.Organism.DarkCallForAction) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Dark Call for action'

        props {
            id = 'dark-section'
            value = '<h2 class="heading mb-3">Free Consultation</h2><p class="sub-heading mb-5 pb_color-light-opacity-8">Far far away, behind the word mountains, far from the countries Vokalia and Consonantia, there live the blind texts.</p>'
            buttonLabel = 'Call us now'
        }
    }

    id = null
    value = null
    buttonLabel = null
    buttonUri = '#'

    renderer = afx`
        <section id={props.id} class="pb_sm_py_cover text-center cover-bg-black cover-bg-opacity-4" style="background-image: url(/_Resources/Static/Packages/Ttree.AfxWorkshop/Images/1900x1200_img_3.jpg)">
            <div class="container">
                <div class="row align-items-center">
                  <div class="col-md-12">
                    {props.value}
                    <Ttree.AfxWorkshop:Component.Atom.Button
                        label={props.buttonLabel}
                        uri={props.buttonUri}
                        @if.hasLabel={String.isBlank(props.buttonLabel) == false}
                    />
                  </div>
                </div>
            </div>
        </section>
    `
}
